plugins {
  id 'groovy'
  id 'maven'
  id 'maven-publish'
  id 'com.gradle.plugin-publish' version '0.10.0'
  id 'java-gradle-plugin'
}

group 'io.ebean'
version '11.27.1-bjornvester-1.0.3'

if (project.hasProperty("signing.keyId")) {
  apply plugin: 'signing'
  signing {
    sign configurations.archives
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
}

dependencies {
  implementation  group: 'io.ebean', name: 'ebean-agent', version: '11.27.1'

  implementation gradleApi()
  implementation localGroovy()

  testImplementation gradleTestKit()
  testImplementation('org.spockframework:spock-core:1.2-groovy-2.5') {
    exclude group: 'org.codehaus.groovy'
  }
}

gradlePlugin {
  plugins {
    ebeanPlugin {
      id = 'io.ebean'
      implementationClass = 'io.ebean.gradle.EnhancePlugin'
    }
  }
}

task groovydocJar(type: Jar) {
  from groovydoc
  archiveClassifier = 'javadoc'
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  archiveClassifier = 'sources'
}

artifacts {
  archives jar
  archives groovydocJar
  archives sourcesJar
}

jar {
  manifest.attributes provider: 'gradle'
}

pluginBundle {
  website = 'https://ebean-orm.github.io/docs/tooling/gradle'
  vcsUrl = 'https://github.com/ebean-orm-tools/ebean-gradle-plugin'
  description = 'Ebean ORM'
  tags = ['Ebean', 'ORM', 'JPA']
  plugins {
    ebeanPlugin {
      id = 'io.ebean'
      displayName = 'Ebean plugin'
    }
  }
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: sonatypeUsername, password: sonatypePassword)
      }

      pom.project {
        name 'ebean-gradle-plugin'
        packaging 'jar'
        description 'Gradle plugin for enhancement Ebean entity beans'
        url 'http://github.com/ebean-orm-tools/ebean-gradle-plugin'

        scm {
          url 'scm:git@github.com/ebean-orm-tools/ebean-gradle-plugin.git'
          connection 'scm:git@github.com/ebean-orm-tools/ebean-gradle-plugin.git'
          developerConnection 'scm:git:https://github.com/ebean-orm-tools/ebean-gradle-plugin.git'
        }

        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
          }
        }

        developers {
          developer {
            id 'rbygrave'
            name 'Robin Bygrave'
          }
          developer {
            id 'vnnv'
            name 'Victor Nikiforov'
          }
          developer {
            id 'bjornvester'
            name 'Bj√∏rn Vester'
          }
        }
      }
    }
  }
}

tasks.named("wrapper").configure {
  distributionType = Wrapper.DistributionType.ALL
  gradleVersion = "5.2"
}
